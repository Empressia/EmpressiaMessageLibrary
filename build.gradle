plugins {
	id "java";
	id "signing";
	id "maven-publish";
}

ext.moduleName = "jp.empressia.message";
ext.junitVersion = "5.7.0";
ext.hamcrestVersion = "2.2";
archivesBaseName = moduleName;

repositories {
	mavenCentral();
}

dependencies {
	// use JUnit test framework.
	testImplementation(group:"org.junit.jupiter", name:"junit-jupiter-api", version:junitVersion);
	testRuntimeOnly(group:"org.junit.jupiter", name:"junit-jupiter-engine", version:junitVersion);
	testImplementation(group:"org.hamcrest", name:"hamcrest", version:hamcrestVersion);
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8";
	options.release = 11;
	options.compilerArgs << "-parameters";
}

java {
	modularity.inferModulePath = true;
	withJavadocJar();
	withSourcesJar();
}

javadoc {
	options.charSet "UTF-8";
	options.encoding "UTF-8";
	options.addBooleanOption("html5", true);
	options.addStringOption("Xdoclint:none", "-quiet");
}

test {
	useJUnitPlatform();
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = "jp.empressia";
			artifactId = moduleName;
			from components.java;
			// for Maven Central.
			pom {
				name = "Empressia Message Library";
				description = "Message Management for Java.";
				url = "https://github.com/Empressia/EmpressiaMessageLibrary";
				licenses {
					license {
						name = "zlib License";
						comments = "If you want to use this with a team (multiple people), select the MIT license.";
					}
					license {
						name = "MIT License";
						comments = "If you want to use this with a team (multiple people), select the MIT license.";
					}
				}
				developers {
					developer {
						// The unique ID of this developer in the SCM.
						id = "Empressia";
					}
				}
				scm {
					connection = "scm:git:https://github.com/Empressia/EmpressiaMessageLibrary.git"
					url = "https://github.com/Empressia/EmpressiaMessageLibrary.git"
				}
			}
		}
	}
	repositories {
		// publishMavenPublicationToLocalNetworkRepository
		maven {
			name = "LocalNetwork";
			url = local_network_repository_url;
		}
		// publishMavenPublicationToMavenCentralRepository
		maven {
			name = "MavenCentral";
			def releasesRepoUrl = sonatype_release_repository_url;
			def snapshotsRepoUrl = sonatype_snapshot_repository_url;
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl;
			credentials {
				username = sonatype_username;
				password = sonatype_password;
			}
		}
	}
}

signing {
	sign publishing.publications.maven;
}
